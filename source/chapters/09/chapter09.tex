\documentclass[class=book, crop=false, oneside]{standalone}
\usepackage[subpreambles=true]{standalone}

\usepackage{../../style}
\usepackage{../../set-citations}

\graphicspath{{./assets/images/}}
\newmintinline{asm}{}

% arara: pdflatex: { synctex: yes, shell: yes }
% arara: latexmk: { clean: partial }
\begin{document}

\chapter{Programmi Assembly comparati}\label{ch:asm}
\begin{fquote}[Linus Torvalds]Talk is cheap. Show me the code.\end{fquote}
Ora andiamo a riprendere alcuni esempi già portati nel capitolo dedicato all'Assembly MIPS e andremo a confrontare le implementazioni proposte con le corrispettive per Assembly x86 e ARM.
Per completezza, specifichiamo che il codice che mostreremo è stato ottenuto con il compilatore \mintinline{batch}{gcc}, lievemente modificato affinché risulti più leggibile (principalmente sull'ordine logico delle istruzioni e sulla nomenclatura dei registri).

\section{Semplici espressioni aritmetico-logiche}
Cominciamo con delle semplici operazioni aritmetiche, che evidenzieranno come le architetture RISC (MIPS e ARM) permettono di eseguire questo tipo di istruzioni in modo molto naturale, al contrario dell'implementazione leggermente più macchinosa di x86. L'espressione è la seguente:
\begin{equation*}
	f = (g + h) - (i + j)
\end{equation*}

\subsubsection{Implementazione MIPS}
In MIPS assumiamo che le variabili \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} vengano mappate nei 4 registri dedicati agli argomenti, rispettivamente \register{\$a0}, \register{\$a1}, \register{\$a2} e \register{\$a3}, e decidiamo di salvare il risultato in \register{\$v0}. A questo punto la conversione Assembly è immediata:
\begin{minted}[linenos, breaklines, tabsize = 4]{asm}
add $a0, $a0, $a1  # memorizza g + h in $a0
add $v0, $a2, $a3  # memorizza i + j in $v0
sub $v0, $a0, $v0  # memorizza (g + h) - (i + j) in $v0
\end{minted}

\subsubsection{Implementazione x86}
Siano \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} mappate rispettivamente in \register{\%rdi}, \register{\%rsi}, \register{\%rcx} e \register{\%rdx} e si voglia salvare il risultato in \register{\%rax}.\\
Il problema di x86 sta nel fatto che le operazioni sono solo a due operandi, con il secondo che funge da destinazione, e quindi non è possibile specificare un diverso registro di destinazione.\\
Possiamo aggirare questa limitazione sfruttando l'istruzione \mintinline{gas}{lea}: questa infatti ha come argomenti \texttt{<addr>, <dst>} e calcola l'indirizzo \texttt{<addr>} secondo base, spiazzamento e indice shiftato, e salva tutto in \texttt{<dst>}. Sarà quindi sufficiente impostare spiazzamento e shift a 0 per riuscire a sommare agevolmente base (per noi \register{\%rdi}) a indice non shiftato (\register{\%rsi}) e salvarne il risultato in \register{\%rax}.
\begin{minted}[linenos, breaklines, tabsize = 4]{gas}
leaq (%rdi, %rsi), %rax  # %rax = g + h con il trick visto sopra
addq %rcx, %rdx          # %rdx = i + j
subq %rdx, %rax          # %rax = (g + h) - (i + j)
\end{minted}

\subsubsection{Implementazione ARM}
Analogamente a MIPS, anche l'implementazione ARM è molto semplice e naturale, una volta mappate \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} in \register{r0}, \register{r1}, \register{r2} e \register{r3} e decidendo di salvare il tutto in \register{r0}.\\
Qui vengono usate \mintinline{asm}{adds} e \mintinline{asm}{subs}, che aggiornano i flag, ma usare le normalissime \mintinline{asm}{add} e \mintinline{asm}{sub} non avrebbe cambiato nulla.
\begin{minted}[linenos, breaklines, tabsize = 4]{arm}
adds r1, r0, r1  ; r1 = g + h
adds r3, r2, r3  ; r3 = i + j
subs r0, r1, r3  ; r0 = (g + h) - (i + j)
\end{minted}

\section{Array e accesso alla memoria}
Ora invece prenderemo come esempio una semplice istruzione che agisce su un array per mostrare diverse modalità di accesso alla memoria: questa volta, x86 permetterà di ridurre notevolmente il numero di righe di codice, mentre le due ISA RISC richiederanno molte più istruzioni.
\begin{minted}{c}
a[12] = h + a[8];
\end{minted}
\subsubsection{Implementazione MIPS}
Come già visto, in MIPS sarà necessario utilizzare le istruzioni di \mintinline{asm}{lw} e \mintinline{asm}{sw} per accedere all'indirizzo dell'array; inoltre, ricordiamo che  lo shift necessario a ottenere l'indirizzo desiderato andrà sempre moltiplicato per lo spiazzamento di 4 byte, sicché ogni parola risulti lunga 32 bit.\\
Assumendo quindi che \mintinline{c}{h} stia in \register{\$a0} e l'indirizzo dell'array \mintinline{c}{a} in \register{\$a1} otteniamo:
\begin{minted}[linenos, breaklines, tabsize = 4]{asm}
lw $v0, 32($a1)    # carica in $v0 il contenuto di a incrementato di 4 * 8
add $a0, $v0, $a0  # salva in $a0 a[8] + h
sw $a0, 48($a1)    # ripone il contenuto di $a0 nell'indirizzo di a incrementato di 4 * 12
\end{minted}

\subsubsection{Implementazione x86}
La capacità di accedere direttamente alla memoria grazie a istruzioni come \mintinline{gas}{addl} e \mintinline{gas}{movl} semplifica di molto questa operazione per l'ISA Intel.\\
Assumendo che  \mintinline{c}{h} stia \register{\%edi} e l'indirizzo di \mintinline{c}{a} in \register{\%rsi}, otteniamo:
\begin{minted}[linenos, breaklines, tabsize = 4]{gas}
addl 32(%rsi), %edi  # %edi = a[8] + h
movl %edi, 48(%rsi)  # sposta a[8] + h in a[12]
\end{minted}

\subsubsection{Implementazione ARM}
La versione ARM è molto simile a quella MIPS.\\
Assumendo che \mintinline{c}{h} stia in \register{r0} e l'indirizzo di \mintinline{c}{a} in \register{r1}, otteniamo:
\begin{minted}[linenos, breaklines, tabsize = 4]{arm}
ldr r3, [r1, #32]  ; carica a[8] in r3
add r0, r3, r0     ; salva in r0 a[8] + h
str r0, [r1, #48]  ; riponi r0 in a[12]
\end{minted}

\section{Blocchi condizionali}
Vediamo ora la gestione di istruzioni condizionali. Solitamente queste vengono gestite da istruzioni di salti condizionati, ma alcune ISA hanno modalità di gestione decisamente peculiari.
\subsection*{Condizioni di uguaglianza}
Andiamo ad analizzare le traduzioni del seguente codice C:
\begin{minted}[linenos, breaklines, tabsize = 4]{c}
if (i == j){
	f = g + h;
} else {
	f = g - h;
}
\end{minted}
\subsubsection{Implementazione MIPS}
In MIPS assumiamo che le variabili \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} vengano mappate nei 4 registri dedicati agli argomenti, rispettivamente \register{\$a0}, \register{\$a1}, \register{\$a2} e \register{\$a3}, e decidiamo di salvare il risultato in \register{\$v0}.\\
Utilizzeremo quindi \mintinline{asm}{bne} per operare un confronto fra due registri general purpose per verificare \mintinline{c}{i == j}; qualora la condizione non fosse verificata, faremo il salto a L2 e alla sottrazione \mintinline{c}{g - h}. Differentemente, verrà eseguita la somma \mintinline{c}{g + h} e un salto incondizionato \mintinline{asm}{j} farà terminare la sequenza.
\begin{minted}[linenos, tabsize = 4]{asm}
  bne $a2, $a3, L2   # salta a L2 se $a2 e $a3 sono diversi
  add $v0, $a0, $a1
  j L3               # salta a L3
L2:
  sub $v0, $a0, $a1
L3:
\end{minted}

\subsubsection{Implementazione x86}
Siano \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} mappate rispettivamente in \register{\%rdi}, \register{\%rsi}, \register{\%rcx} e \register{\%rdx} e si voglia salvare il risultato \mintinline{c}{f} in \register{\%rax}.\\
L'implementazione risulta più complessa rispetto a quella di MIPS, poiché dovremo usare \mintinline{gas}{lea} al posto di \mintinline{gas}{add} e sopratto dovremo affiancare alla \mintinline{gas}{sub} una \mintinline{gas}{mov} per far sì che il risultato vada salvato in \register{\%rax}. Inoltre, è necessaria una \mintinline{gas}{cmpq} per settare i flag e solo dopo possiamo lanciare l'istruzione di salto.
\begin{minted}[linenos, tabsize = 4]{gas}
  cmpq %rcx, %rdx  # confronta i e j e setta il flag
  jne L2           # se i != j, salta a L2
  leaq (%rdi, %rsi), %rax  # salva in %rax g + h
  jmp L3
L2:
  movq %rdi, %rax  # sposta g da %rdi a %rax
  subq %rsi, %rax  # salva in %rax
L3:
\end{minted}
Tutto questo può però essere semplificato dalla magica istruzione \mintinline{gas}{cmov} (\mintinline{gas}{mov} condizionale) di x86:
\begin{minted}[linenos, tabsize = 4]{gas}
leaq (%rdi, %rsi), %rax  # salva g + h in %rax
subq %rdi, %rsi          # salva g - h in %rsi
cmpq %rcx, %rdx          # i == j
cmovne %rsi, %rax        # sposta %rsi in %rax se il i è diverso da j
\end{minted}

\subsubsection{Implementazione ARM}
L'implementazione ARM ci permette di evitare istruzioni di salto grazie alla sua versione condizionale delle operazioni aritmetiche. Assumendo che \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} e \mintinline{c}{j} vengano mappate rispettivamente in \register{r0}, \register{r1}, \register{r2} e \register{r3} e che il risultato vada in \register{r0}, otteniamo:
\begin{minted}[linenos]{arm}
cmp r2, r3         ; setta i flag
addeq r0, r0, r1   ; se i == j, salva g + h in r0
subne r0, r0, r1   ; altrimenti, salva g - h in r0
\end{minted}

\subsection*{Condizioni di disuguaglianza}
Vediamo ora invece una condizione di disuguaglianza:
\begin{minted}[linenos, breaklines, tabsize = 4]{c}
if (i < j){
	f = g + h;
} else {
	f = g - h;
}
\end{minted}
\subsubsection{Implementazione MIPS}
Assumendo che le variabili vengano mappate come sopra, il codice MIPS non appare troppo diverso, se non per l'utilizzo dell'istruzione \mintinline{asm}{slt}:
\begin{minted}[linenos, tabsize = 4]{asm}
  slt $a2, $a2, $a3   # setta $a2 se i < j
  beq $a2, $zero, L2  # se $a2 == 0 (non settato), vai a L2
  add $v0, $a0, $a1
  j L3                # salta a L3
L2:
  sub $v0, $a0, $a1
L3:
\end{minted}
\subsubsection{Implementazione x86}
L'implementazione è quasi identitica a quella della condizione di uguaglianza, cambia solamente l'istruzione di salto.
\begin{minted}[linenos, tabsize = 4]{gas}
  cmpq %rcx, %rdx  # confronta i e j e setta il flag
  jge L2           # se i < j, salta a L2
  leaq (%rdi, %rsi), %rax  # salva g + h in %rax
  jmp L3
L2:
  movq %rdi, %rax  # sposta g da %rdi a %rax
  subq %rsi, %rax  # salva g - h in %rax
L3:
\end{minted}
Anche l'implementazione con \mintinline{gas}{cmov} è simile:
\begin{minted}[linenos, tabsize = 4]{gas}
leaq (%rdi, %rsi), %rax  # salva g + h in %rax
subq %rsi, %rax          # salva g - h in %rax
cmpq %rcx, %rdx          # flag
cmovge %rdi, %rax        # sposta %rdi in %rax se il i < j
\end{minted}

\subsubsection{Implementazione ARM}
Ancora una volta, le indicazioni condizionali delle istruzioni ARM ci semplificano la vita:
\begin{minted}[linenos]{arm}
cmp r2, r3         ; setta i flag
addlt r0, r0, r1   ; se i < j, salva g + h in r0
subge r0, r0, r1   ; altrimenti, salva g - h in r0
\end{minted}

\section{Cicli}
Ora analizziamo l'implementazione di un ciclo con condizione di terminazione basata sui valori contenuti in un array di interi. Vedremo che l'implementazione usata sarà sempre quella del salto condizionale, nonostante ARM coi suoi suffissi potrebbe prestarsi ad implementazioni alternative. Il codice C è il seguente:
\begin{minted}[linenos, breaklines, tabsize = 4]{c}
int i = 0;
while (a[i] == k){
	i += 1;
}
\end{minted}

\subsubsection{Implementazione MIPS}
In MIPS mappiamo \mintinline{c}{k} in \register{\$a0}, l'indirizzo di \mintinline{c}{a} in \register{\$a1} e \mintinline{c}{i} in \register{\$v0}. Notiamo che per poter operare il confronto \mintinline{c}{a[i]==k} sarà necessario servirsi di un registro temporaneo \register{\$t0} di appoggio dove caricare l'elemento del vettore \mintinline{c}{a}; inoltre, poiché le modalità d'indirizzamento di MIPS sono molto poco potenti, sarà necessario operare lo shift logico a sinistra a ogni ciclo per mantenere la regolarità delle words.
\begin{minted}[linenos, tabsize = 4]{asm}
start:
	move $v0, $zero    # i = 0
L1:
	sll $t1, $v0, 2    # salva 4 * i in $t1
	add $t1, $t1, $a1  # ottieni l'indirizzo di a[i]
	lw $t0, 0($t1)     # carica in $t0 il contenuto di a[i]
	bne $t0, $a0, L2   # salta a l2 se a[i] è diverso da k
	addi $v0, $v0, 1
	j L1
L2:
\end{minted}

L'implementazione di \mintinline{batch}{gcc} è leggermente più complessa e aggira la scarsa potenza sull'indirizzamento di MIPS usando non uno, ma due registri come contatori: \register{\$v0}  contiene \mintinline{c}{i} e \register{\$a1} contiene il contatore perl'elemento successivo dell'array; quest'ultimo registro inoltre viene sempre incrementato di 4, assumendo che ogni elemento dell'array, in quanto \mintinline{c}{int}, abbia dimensione 4 byte.
\begin{minted}[linenos, tabsize = 4, breaklines]{asm}
start:
	lw $a2, 0($a1)    # carica in a2 il contenuto di a[0]
	bne $a2, $a0, L2  # se a[0]==k, salta a L2
	addi $a1, $a1, 4  # incrementa a di 4
	move $v0, $zero   # inizializza il contatore
L1:
	addi $a1, $a1, 4  # incrementa a di 4
	lw $v1, -4($a1)   # carica in v1 il contenuto di a decrementato di 4
	addi $v0, $v0, 1  # incrementa il contatore
	beq $v1, $a0, L1  # se il contenuto di a in questa posizione è uguale k torna al ciclo
	j L3              # termina
L2:
	move $v0, $zero   # inizializza il contatore
L3:
\end{minted}

\subsubsection{Implementazione x86}
L'implementazione di Intel è più compatta. Mappiamo le variabili come segue: \mintinline{c}{i} in \register{\%rax}, l'indirizzo di base \mintinline{c}{a} in \register{\%rsi} e \mintinline{c}{k} in \register{\%edi}; notiamo che possiamo mappare \mintinline{c}{k} in un registro a 32 bit poiché andrà confrontato con un elemento di un array, che abbiamo detto essere lungo 4 byte.
Notiamo inoltre che la \mintinline{gas}{cmpl} alla riga \(7\) permette di confrontare \mintinline{c}{k} direttamente con un elemento in memoria, senza bisogno di operazioni di load; inoltre la stessa istruzione ci permette di moltiplicare per 4 l'indice in \register{\%rax}, senza bisogno di un ulteriore registro di appoggio.
\begin{minted}[linenos, tabsize = 4]{gas}
start:
	cmpl (%rsi), %edi  # confronta k e il primo elemento di a
	jne L2             # se non sono uguali, vai a L2
	movq $0, %rax      # inizializza l'indice
L1:
	addq $1, %rax               # incrementa i
	cmpl %edi, (%rsi, %rax, 4)  # confronta k con a[i]
	je L1                       # se k==a[i], torna a L1
	jmp L3                      # salto incondizionato a L3
L2:
	movq $0, %rax
L3:
\end{minted}

\subsubsection{Implementazione ARM}
Assumiamo che le variabili siano mappate in questo modo: \mintinline{c}{k} in \register{r0}, l'indirizzo di \mintinline{c}{a[0]} inizialmente in \register{r1} e \mintinline{c}{i} in \register{r3}. Notiamo nella riga 8 l'utilizzo dell'indirizzamento \emph{pre indexing}, che incrementa \register{r1} di 4 prima di accedervi e come siano utilizzati due registri, \register{r3} e \register{r2} per il valore del contatore e della locazione di memoria cui accedere.

Si noti che in \texttt{L2} viene settato a 0 il valore del contatore: questa operazione risulta necessaria in quanto se il flusso d'esecuzione arriva a \texttt{L2} sicuramente non si è entrati nel ciclo (e dunque non è avvenuta l'inizializzazione del puntatore).
\begin{minted}[linenos,tabsize = 4]{arm}
start:
	ldr r3, [r1]     ; carica a[0] in r3
	cmp r0, r3       ; confronta a[0] con k
	bne L2           ; se sono diversi, salta a L2
	mov r3, #0       ; ora r3 contatore: inizializza i=0
L1:
	add r3, r3, #1   ; incrementa i
	ldr r2 [r1, #4]! ; carica a[i] in r2
	cmp r2, r0       ; confronta a[i] con k
	beq L1           ; se sono uguali torna al ciclo
	b L3             ; altrimenti salta a L3
L2:
	mov r3, #0       ; setta il contatore a 0
L3:
	mov r0, r3
\end{minted}



\section{Invocazione di subroutine}
I prossimi esempi serviranno ad esaminare le ABI e le convenzioni di chiamata delle tre architetture che stiamo analizzando.
\subsection*{Funzioni foglia}
Iniziamo con una funzione foglia, ossia una funzione che non chiama alcuna subroutine, che conseguentemente non rende necessario salvare il \texttt{return address register}. Di seguito il codice C.
\begin{minted}[linenos,tabsize = 4]{c}
int esempio_foglia(int g, int h, int i, int j){
	int f;
	f = (g + h) - (i + j);
	return f;
}
\end{minted}

\subsubsection{Implementazione MIPS}
Mappatura: \mintinline{c}{g}, \mintinline{c}{h}, \mintinline{c}{i} \mintinline{c}{j} in \register{\$a0}, \register{\$a1}, \register{\$a2}, \register{\$a3}, e il valore di ritorno va salvato in \register{\$v0}. Nulla di particolare da segnalare, se non il ritorno implementato con un salto incondizionato all'indirizzo del \texttt{return address register}.
\begin{minted}[linenos,tabsize = 4]{asm}
esempio_foglia:
	addu $a0, $a0, $a1  # somma g + h e salva in a0
	addu $v0, $a2, $a3  # somma  i + j  e salva in v0
	subu $v0, $a0, $v0  # sottrae a0 a v0
	jr $ra              # salto all'indirizzo di ritorno
\end{minted}

\subsubsection{Implementazione x86}
L'implementazione Intel è immediata, se si tiene conto del fatto che le variabili verranno mappate nei "registri \register{e}" (\register{\%edi}, \register{\%esi}, \register{\%ecx}, \register{\%edx}) a 32 bit, anziché nei "registri \register{r}" (\register{\%rdi}, \register{\%rsi}, \register{\%rcx}, \register{\%rdx}) offerti da x86-64; questo succede perché i valori che dobbiamo modificare sono di tipo \mintinline{c}{int}, quindi di dimensione appunto 32 bit. Notiamo inoltre l'istruzione \texttt{ret} preposta a restituire il ritorno della subroutine.
\begin{minted}[linenos,tabsize = 4]{gas}
esempio_foglia:
	leal (%rdi, %rsi), %eax  # salva in eax g + h
	addl %ecx, %edx          # salva in edx i + j
	subl %edx, %eax          # sottrae le due somme
	ret
\end{minted}

\subsubsection{Implementazione ARM}
Anche l'implementazione ARM è molto semplice. Notiamo l'utilizzo di \texttt{rsb}, che sottrae l'operando di destra a quello centrale, e l'uso di \texttt{bx} (una macro per \mintinline{arm}{move r15, r}) per saltare all'indirizzo contenuto nel link register \texttt{lr}.
\begin{minted}[linenos,tabsize = 4]{arm}
esempio_foglia:
	add r0, r0, r1  ; salva in r0 la somma g + h
	add r3, r2, r3  ; salva in r3 la somma i + j
	rsb r0, r3, r0  ; salva in r0 il risultato di r0 - r3
	bx lr
\end{minted}

\subsection*{Funzioni non foglia}
Com'era lecito aspettarsi, l'implementazione di funzioni non foglia (che invocano altre funzioni) è più complessa. Analizziamo un esempio semplice:
\begin{minted}[linenos,breaklines, tabsize = 4]{c}
int inc(int n){
	return n + 1;
}

int f(int x){
	return inc(x) - 4;
}
\end{minted}

\subsubsection{Implementazione MIPS}
La conversione di \mintinline{c}{inc} è banale, \mintinline{c}{f} invece ha notevoli complicazioni; è necessario infatti salvare il contenuto di \register{\$ra} prima di chiamare \mintinline{c}{inc}, e possiamo farlo decrementando di una word (4 byte) lo stack pointer per così salvare \register{\$ra} in \register{\$sp} nello spazio appena creato, per poi recuperarlo (e reincrementare \register{\$sp}) prima del ritorno.
\begin{minted}[linenos, breaklines, tabsize = 4]{asm}
inc:
	addiu $v0, $a0, 1   # incrementa
	jr $ra

f:
	addiu $sp, $sp, -4  # decrementa di 4 byte sp
	sw $ra, 0($sp)      # salva ra all'indirizzo di sp
	jal inc             # chiama inc
	addiu $v0, $v0, -4  # sottrae 4 a v0
	lw $ra, 0($sp)      # recupera ra
	addiu $sp, $sp, 4   # ripristina lo stack pointer
	jr $ra              # ritorna
\end{minted}

L'implementazione di \mintinline{batch}{gcc} è ancora diversa, perché l'ABI da noi usata prevede che il record di attivazione sia un multiplo di 8, per cui \register{\$sp} verrà decrementato di 8 bytes; altre ABI avrebbero lavorato ancora diversamente, dal momento che la gestione dello stack è appunto demandata alla \emph{Application Binary Interface} utilizzata.
\begin{minted}[linenos, breaklines, tabsize = 4]{asm}
inc:
	addiu $v0, $a0, 1
	jr $ra

f:
	addiu $sp, $sp, -8  # sp viene decrementato di 8
	sw $ra, 4($sp)      # ra viene salvato in sp con offset di 4
	jal inc
	addiu $v0, $v0, -4
	lw $ra, 4($sp)      # recupero ra
	addiu $sp, $sp, 8   # ripristino sp
	jr $ra
\end{minted}

\subsubsection{Implementazione x86}
Fortunatamente per noi, le funzioni \mintinline{gas}{call} e \mintinline{gas}{rec} di x86 gestiscono magistralmente il movimento dello stack pointer e non richiedono alcun intervento extra da parte nostra; inoltre, la sempre fresca \mintinline{gas}{lea} ci regala la libertà di cui abbiamo bisogno per salvare i risultati di una somma in un operando terzo.
\begin{minted}[linenos, breaklines, tabsize = 4]{gas}
inc:
	leal 1(%rdi), %eax  # salva rdi + 1 in eax
	ret

f:
	call inc            # chiama inc
	subl $4, %eax       # sottrae 4 a eax
	ret
\end{minted}

\subsubsection{Implementazione ARM}
ARM si rivela molto interessante: le sue possibilità di indirizzament \emph{pre indexing} ci consentono di effettuare il decremento dello stack pointer e il salvataggio del link register in una sola riga di codice; inoltre, dal momento che in ARM \register{PC} è un registro referenziabilie come fosse un general purpose, possiamo direttamente prelevare il link register dallo stack e caricarlo direttamente nel program counter e, sempre nella stessa riga, reincrementare \register{SP} di 4 grazie all'indirizzamento \emph{post indexing}.
\begin{minted}[linenos, breaklines, tabsize = 4]{arm}
inc:
	add r0, r0, #1
	bx lr

f:
	str lr, [sp, #-4]!  ; decrementa SP di 4 e salva lr
	bl inc              ; chiama inc
	sub r0, r0 #4       ; sottrae 4 a r0
	ldr pc, [sp], #4    ; carica lr in PC e recupera SP
\end{minted}

\section{Copia stringa}
Infine consideriamo una funziona che copia un array di caratteri:
\begin{minted}[linenos, breaklines, tabsize = 4]{c}
void copia_stringa(char *d , const char *s){
	int i = 0;
	while((d[i] = s[i]) != 0){
		i += 1;
	}
}
\end{minted}

\subsubsection{Implementazione MIPS}
Abbiamo già discusso l'implementazione MIPS di questa funzione in~\ref{sec:mipstring}, per cui qui riporteremo solamente il codice, generato da \mintinline{batch}{gcc}:
\begin{minted}[linenos, breaklines, tabsize = 4]{asm}
copia_stringa:
	lb $v0, 0($a1)      # carica in v0 il contenuto di s[0]
	sb $v0, 0($a0)      # salva v0 in d[0]
	beq $v0, $zero, L5  # se v0 == 0, salta a L5
	move $v0, $zero     # inizializza v0 a 0
L3:
	addiu $v0, $v0, 1   # incrementa il contatore v0
	addu $v1, $a1, $v0  # salva s[0 + contatore] in v1
	lb $v1, 0($v1)      # carica *(s + contatore) in v1
	addu $a2, $a0, $v0  # salva d[0 + contatore] in a2
	sb $v1, 0($a2)      # ripone v1 in *(d + contatore)
	bne $v1, $zero, L3  # se v1 non è il carattere nullo torna al ciclo
L5:
	jr $ra              # termina e ritorna
\end{minted}

\subsubsection{Implementazione x86}

\begin{minted}[linenos, breaklines, tabsize = 4]{gas}
copia_stringa:
	movzbl (%rsi), %eax
	movb %al, (%rdi)
	testb %al, %al
	je L1                      # se uguali, ritorna e termina
	movl $0, %eax              # inizializza a 0 eax
L3:
	addl $1, %eax              # incrementa il contatore
	movslq %eax, %rcx
	movzbl (%rsi, %rcx), %edx
	movb %dl, (%rdi, %rcx)
	testb %dl, %dl
	jne L3
L1:
	ret
\end{minted}

\subsubsection{Implementazione ARM}
Ecco invece il codice ARM; notiamo come i suffissi condizionali  e soprattutto il \emph{pre indexing} vengono sfruttati per snellire un codice in realtà molto simile a quello di MIPS.
\begin{minted}[linenos, breaklines, tabsize = 4]{arm}
copia_stringa:
	ldrb r3, [r1]       ; carica s[0] in r3
	strb r3, [r0]       ; salva r3 in d[0]
	cmp r3, #0          ; r3 è 0?
	bxeq lr             ; se sì, ritorna e termina
L3:
	ldrb r3, [r1, #1]!  ; carica in r3 il contenuto di s[] incrementato di 1 e salva le modifiche
	strb r3, [r0, #1]!  ; salva r3 in d[] incrementato di 1 e salva le modifiche
	cmp r3, #0          ; la stringa è terminata?
	bne L3              ; se no, torna al ciclo
	bx lr               ; altrimenti ritorna e termina
\end{minted}


\end{document}
